
buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
}


def rootPath = "${rootDir}/lib-content-editor/"
def rootTemplatePath = rootPath + "templates/"
def umEditorAssetsPath = "${rootDir}/app-android/src/main/assets/http/umEditor"

def umJsDynamicTasks = []
def umCssDynamicTasks = []
def umJsTaskIndex = 1
def umCssTaskIndex = 1
def testTimeout = 6000

/*Task which copies all umEditor resources from lib-content-editor directory to android assets*/
task copyResourcesToAndroidAssets << {
    def tinyMce = "lib/tinymce"
    def fonts = "lib/material-icon"
    def locale = "locale"
    def templates = "/templates"

    copy { from rootTemplatePath into umEditorAssetsPath + templates }
    copy { from rootPath + tinyMce into umEditorAssetsPath +"/"+ tinyMce }
    copy { from rootPath + fonts into umEditorAssetsPath +"/"+fonts }
    copy { from rootPath + locale into umEditorAssetsPath +"/"+locale }

    delete rootPath + "dist"
    delete rootTemplatePath + "umEditorBlankDoc.zip"
    delete umEditorAssetsPath + templates + "/template_page.html"
    delete umEditorAssetsPath + templates + "/umEditorBlankDoc"

}


/* Task which creates blankDocument to be used as blank document template*/
task createUmEditorBlankDocumentArchive(type: Zip) {
    def blankDocPath = rootTemplatePath + "umEditorBlankDoc"

    from blankDocPath
    archiveName 'umEditorBlankDoc.zip'
    destinationDir(file(rootTemplatePath))

    doLast {
       delete blankDocPath
    }
}
copyResourcesToAndroidAssets.dependsOn(createUmEditorBlankDocumentArchive)


/*Task which creates all directories necessary for preparing a blank document template*/
task prepareUmEditorDirectories << {
    def blankDocPath = rootTemplatePath + "umEditorBlankDoc/"

    copy { from rootTemplatePath + "template_page.html",
            rootPath + "src/epub/container.xml",
            rootPath + "src/epub/content.opf",
            rootPath + "src/epub/nav.xhtml",
            rootPath + "dist/css/UmEditorCore.min.css",
            rootPath + "/lib/jquery/jquery3.3.1.min.js",
            rootPath + "dist/js/UmEditorCore.min.js",
            rootPath + "dist/js/UmWidgetManager.min.js" into blankDocPath }
}
createUmEditorBlankDocumentArchive.dependsOn(prepareUmEditorDirectories)


/* Task to minify UmEditor javascript file*/
new File(rootPath+"src/js").eachFile { def file ->
    def dynamicTaskName = "taskMinifyJs${umJsTaskIndex}"
    def jsDistDir = rootPath + "dist/js/"
    task "${dynamicTaskName}"(type: Exec) {
        def sourceFile = file.absolutePath
        def destFile = jsDistDir + "${file.name.replaceFirst(~/\.[^\.]+$/, '')}.min.js"
        File jsDistFile = new File(jsDistDir)
        jsDistFile.mkdirs()
        commandLine 'uglifyjs', sourceFile, '--compress', '--comments','--keep-fnames', '-o', destFile
    }
    umJsDynamicTasks << dynamicTaskName
    umJsTaskIndex++
}
task minifyUmEditorJs(dependsOn: umJsDynamicTasks) << {}


/* Task to minify UmEditor css files*/
new File(rootPath +"src/css").eachFile { def file ->
    def dynamicTaskName = "taskMinifyCss${umCssTaskIndex}"
    def cssDistDir = rootPath + "dist/css/"
    task "${dynamicTaskName}"(type: Exec) {
        def sourceFile = file.absolutePath
        def destFile = cssDistDir  + "${file.name.replaceFirst(~/\.[^\.]+$/, '')}.min.css"
        File cssDistFile = new File(cssDistDir)
        cssDistFile.mkdirs()
        commandLine 'cleancss', '-o', destFile, sourceFile
    }
    umCssDynamicTasks << dynamicTaskName
    umCssTaskIndex++
}
task minifyUmEditorCss(dependsOn: umCssDynamicTasks) << {}
prepareUmEditorDirectories.dependsOn(minifyUmEditorCss,minifyUmEditorJs)


/* Execute content template tests */
task runContentTemplatesTest(type: Exec){
    def testFilePath = rootPath + "tests/content-template-tests.html"
    commandLine 'mocha-chrome', testFilePath, '--timeout ',testTimeout
}


/* Execute content formatting tests */
task runContentFomattingTests (type: Exec){
    def testFilePath = rootPath + "tests/content-formatting-tests.html"
    commandLine 'mocha-chrome', testFilePath, '--timeout ',testTimeout
}


/* Run all UmEditor Javascript tests */
task test(dependsOn: [runContentFomattingTests, runContentTemplatesTest]) << {}

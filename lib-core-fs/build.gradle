apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: "maven-publish"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compile project(":core")
    testCompile project(":sharedse")
    testCompile project(':lib-core-fs-tests')
    testCompile project(":sharedse-tests")
    testCompile project(":app-javase")
    implementation project(":lib-database")
    implementation project(":lib-database-runtime")
    compileOnly project(':lib-database-entities')

    implementation "org.json:json:$version_json"
    implementation "net.lingala.zip4j:zip4j:$version_zip4j"
    ext.testDbDependency = rootProject.ext.buildConfigProperties['test.jdbc.dbDriverDependency']
    testCompile "$testDbDependency"
    testCompile "com.squareup.okhttp3:okhttp:$version_okhttp"
}


jar {
    //Exclude factory classes that are intended to be replaced by platform specific implementations
    exclude("com/ustadmobile/core/fs/db/HttpCacheDbManagerFactory.class")
}


sourceCompatibility = "1.8"
targetCompatibility = "1.8"


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version rootProject.version

            from components.java

            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}



apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: "maven-publish"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
    maven {
        url "http://devserver2.ustadmobile.com/repo-mvn"
    }

}

/*
 * Added to overcome limitation on Android studio as per:
 *  http://tools.android.com/knownissues#TOC-JUnit-tests-missing-resources-in-classpath-when-run-from-Studio
 */
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    from "${projectDir}/../core-tests/src/main/resources"
    from "${projectDir}/../sharedse-tests/src/main/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

dependencies {
    //compile fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.nanohttpd:nanohttpd:$version_nanohttpd"
    implementation "org.nanohttpd:nanohttpd-nanolets:$version_nanohttpd"
    implementation "com.ustadmobile.qrcodelibomrfork:qrcode-core:$version_qrcodelibfork"
    implementation "com.squareup.okhttp3:okhttp:$version_okhttp"
    implementation "net.sf.kxml:kxml2:$version_kxml";
    implementation "com.google.code.gson:gson:$version_gson"
    implementation "net.lingala.zip4j:zip4j:$version_zip4j"
    implementation "org.json:json:$version_json"

    //jsuop
    implementation "org.jsoup:jsoup:$version_jsoup"

    implementation project(":core")
    testImplementation project (':lib-database-jdbc')
    implementation project(":lib-database")
    implementation project(":lib-database-runtime")
    compileOnly project(':lib-database-entities')
    implementation project(':lib-core-fs')
    implementation project(":lib-util")

    testImplementation "junit:junit:$version_junit"
    testImplementation "org.mockito:mockito-core:$version_android_mockito"
    testImplementation project(":sharedse-tests")
    testImplementation project(":core-tests")
    testImplementation project (':lib-database-entities')


    //SQLite dependencies for running db-driven tests
    testImplementation "com.github.h-thurow:simple-jndi:$version_simple_jndi"
    testImplementation "org.apache.commons:commons-pool2:$version_apache_commons_pool2"
    testImplementation "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version rootProject.version

            from components.java

            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}
